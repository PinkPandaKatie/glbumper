dnl Init.
AC_INIT(glbumper,0.0.1)
AC_PREREQ(2.50)
AC_CONFIG_SRCDIR(README)

dnl Detect the canonical host and target build environment
AC_CANONICAL_HOST
AC_CANONICAL_TARGET

dnl Setup for automake
AM_INIT_AUTOMAKE
AM_CONFIG_HEADER(config.h)

dnl Checks for programs.
AC_PROG_MAKE_SET
AC_PROG_CC
AC_PROG_CPP
AC_PROG_CXX
AC_PROG_INSTALL
AC_PROG_RANLIB

dnl Check for SDL
SDL_VERSION=1.2.0
AM_PATH_SDL($SDL_VERSION,
            :,
	    AC_MSG_ERROR([*** SDL version $SDL_VERSION not found!])
)
LIBS="$LIBS $SDL_LIBS"
CFLAGS="$SDL_CFLAGS"

dnl Checks for header files.

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_INLINE
AC_TYPE_SIZE_T
AC_STRUCT_TM


AC_MSG_CHECKING(if environ can be included)
AC_TRY_LINK([#include <unistd.h>
#include <stdlib.h>],[*environ;],
[AC_MSG_RESULT(yes);AC_DEFINE(ENVIRON_INCLUDED,1,[environ can be included])],AC_MSG_RESULT(no))
AC_MSG_CHECKING(if environ can be linked)
AC_TRY_LINK([extern char ** environ;],[*environ;],
[AC_MSG_RESULT(yes);AC_DEFINE(ENVIRON_LINKED,1,[environ can be linked])],AC_MSG_RESULT(no))

dnl Checks for libraries.

#Check if the compiler support attributes

#Features to enable/disable

dnl AC_MSG_CHECKING(for SDL_image) 
AC_CHECK_LIB(SDL_image, IMG_Load, GLIBS="$GLIBS -lSDL_image", AC_MSG_ERROR([*** You need SDL_image version 1.2.3]) , )
AC_CHECK_LIB(SDL_mixer, Mix_OpenAudio, GLIBS="$GLIBS -lSDL_mixer", AC_MSG_ERROR([*** You need SDL_mixer]) , )
			  

dnl AC_MSG_CHECKING(for OpenGL) 
case "$ac_cv_target" in
    *-*-cygwin* | *-*-mingw32*)
         GL_H_PREREQ='#include <windows.h>'
	 OPENGL_LIB=opengl32
         GLIBS="$GLIBS -lopengl32 -lglu32"
	 GZIP_LIB="zlib1"
       ;;
    *)
       GL_H_PREREQ=
       OPENGL_LIB=GL
       GLIBS="$GLIBS -lGL -lGLU"
       GZIP_LIB="z"
       ;;
esac

AC_CHECK_LIB($GZIP_LIB, gzopen, LIBS="$LIBS -l$GZIP_LIB", AC_MSG_ERROR([*** You need zlib version 1.2]) , )

AC_CHECK_LIB($OPENGL_LIB, main, have_gl_lib=yes, have_gl_lib=no , )
AC_CHECK_HEADER(GL/gl.h, have_gl_h=yes , have_gl_h=no , $GL_H_PREREQ )

if test x$have_gl_h = xno -o x$have_gl_lib = xno ; then 
  AC_MSG_ERROR([*** opengl not available])
fi

NETLIBS=""
dnl Some host detection and actions for them
case "$target" in
    *-*-cygwin* | *-*-mingw32*)
       NETLIBS="-lwsock32"
       dnl LIBS="$LIBS"
       ;;
    *-*-darwin*)
       dnl We have a problem here: both MacOS X and Darwin report 
       dnl the same signature "powerpc-apple-darwin*" - so we have
       dnl to do more to distinguish them.
       dnl For now I am lazy and do not add proper detection code.
       AC_DEFINE(MACOSX, 1, [Compiling on Mac OS X])
       LIBS="$LIBS -framework AudioUnit"
       ;;
    *-*-linux-gnu*)
       AC_DEFINE(LINUX, 1, [Compiling on GNU/Linux])
       ;;
esac

# On Windows, the server should be a console application, but
# SDL wants to make it a gui app.
GLIBS="$LIBS $GLIBS"
CLIBS=`echo $LIBS | sed 's/-mwindows//;s/-lSDLmain//'`

LIBS=""


single_LDADD="$GLIBS"
client_LDADD="$GLIBS $NETLIBS"
server_LDADD="$CLIBS $NETLIBS"
hybrid_LDADD="$GLIBS $NETLIBS"

AC_SUBST(single_LDADD)
AC_SUBST(client_LDADD)
AC_SUBST(server_LDADD)
AC_SUBST(hybrid_LDADD)


AC_OUTPUT([ 
Makefile src/Makefile
])
